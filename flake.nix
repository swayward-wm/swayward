# This flake file is community maintained
{
  description = "Swayward: An i3-compatible Wayland compositor based on niri.";

  inputs.nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";

  outputs =
    {
      self,
      nixpkgs,
    }:
    let
      revision = self.shortRev or self.dirtyShortRev or "unknown";
      swayward-package =
        {
          lib,
          cairo,
          dbus,
          libGL,
          libdisplay-info_0_3,
          libinput,
          seatd,
          libxkbcommon,
          libgbm,
          pango,
          pipewire,
          pkg-config,
          rustPlatform,
          systemd,
          wayland,
          installShellFiles,
          perl,
          python3,
          which,
          withDbus ? true,
          withSystemd ? true,
          withScreencastSupport ? true,
          withDinit ? false,
        }:

        rustPlatform.buildRustPackage {
          pname = "swayward";
          version = revision;

          src = lib.fileset.toSource {
            root = ./.;
            fileset = lib.fileset.unions [
              ./swayward-config
              ./swayward-ipc
              ./swayward-visual-tests
              ./contrib
              ./docs
              ./proptest-regressions
              ./resources
              ./src
              ./tests
              ./build.rs
              ./Cargo.toml
              ./Cargo.lock
              ./README.md
            ];
          };

          postPatch = ''
            patchShebangs resources/swayward-session contrib/sway-to-kdl
            substituteInPlace resources/swayward.service \
              --replace-fail 'ExecStart=swayward' "ExecStart=$out/bin/swayward"
          '';

          cargoLock = {
            # NOTE: This is only used for Git dependencies
            allowBuiltinFetchGit = true;
            lockFile = ./Cargo.lock;
          };

          strictDeps = true;

          nativeBuildInputs = [
            rustPlatform.bindgenHook
            pkg-config
            installShellFiles
          ];

          nativeCheckInputs = [
            perl
            python3
            which
          ];

          buildInputs =
            [
              cairo
              dbus
              libGL
              libdisplay-info_0_3
              libinput
              seatd
              libxkbcommon
              libgbm
              pango
              python3
              wayland
            ]
            ++ lib.optional (withDbus || withScreencastSupport || withSystemd) dbus
            ++ lib.optional withScreencastSupport pipewire
            # Also includes libudev
            ++ lib.optional withSystemd systemd;

          buildFeatures =
            lib.optional withDbus "dbus"
            ++ lib.optional withDinit "dinit"
            ++ lib.optional withScreencastSupport "xdp-gnome-screencast"
            ++ lib.optional withSystemd "systemd";
          buildNoDefaultFeatures = true;
          cargoBuildFlags = [
            "--package"
            "swayward"
            "--package"
            "swayward-ipc"
          ];

          # The test suite runs an actual swayward instance with a mock backend,
          # which creates a real socket file and therefore needs a runtime directory.
          preCheck = ''
            export XDG_RUNTIME_DIR="$(mktemp -d)"
          '';

          checkFlags = [
            # These tests require the ability to access a "valid EGL Display", but that won't work
            # inside the Nix sandbox.
            "--skip=::egl"
            "--skip=larger_shm_pool_is_accepted"
          ];

          postInstall =
            ''
              installShellCompletion --cmd swayward \
                --bash <($out/bin/swayward completions bash) \
                --fish <($out/bin/swayward completions fish) \
                --nushell <($out/bin/swayward completions nushell) \
                --zsh <($out/bin/swayward completions zsh)

              install -Dm755 contrib/sway-to-kdl $out/bin/swayward-sway-to-kdl
              install -Dm644 resources/swayward.desktop -t $out/share/wayland-sessions
              install -Dm644 resources/swayward-portals.conf -t $out/share/xdg-desktop-portal
              test -x $out/bin/swaywardmsg
              test -x $out/bin/swayward-sway-to-kdl
            ''
            + lib.optionalString withSystemd ''
              install -Dm755 resources/swayward-session $out/bin/swayward-session
              install -Dm644 resources/swayward{.service,-shutdown.target} -t $out/lib/systemd/user
            '';

          env = {
            # Force linking with libEGL and libwayland-client so they end up in RPATH and
            # can be discovered by `dlopen()`
            RUSTFLAGS = toString (
              map (arg: "-C link-arg=" + arg) [
                "-Wl,--push-state,--no-as-needed"
                "-lEGL"
                "-lwayland-client"
                "-Wl,--pop-state"
              ]
            );
            SWAYWARD_BUILD_COMMIT = revision;
          };

          passthru = {
            providedSessions = [ "swayward" ];
          };

          meta = {
            description = "i3-compatible Wayland compositor based on niri";
            homepage = "https://github.com/martintrojer/swayward";
            license = lib.licenses.gpl3Only;
            mainProgram = "swayward";
            platforms = lib.platforms.linux;
          };
        };

      inherit (nixpkgs) lib;
      # Support all Linux systems that the nixpkgs flake exposes
      systems = lib.intersectLists lib.systems.flakeExposed lib.platforms.linux;

      forAllSystems = lib.genAttrs systems;
      nixpkgsFor = forAllSystems (system: nixpkgs.legacyPackages.${system});
    in
    {
      checks = forAllSystems (system: {
        # We use the debug build here to save a bit of time
        inherit (self.packages.${system}) swayward-debug;
      });

      devShells = forAllSystems (
        system:
        let
          pkgs = nixpkgsFor.${system};
          rustfmt' = pkgs.rustfmt.override { asNightly = true; };
          inherit (self.packages.${system}) swayward;
        in
        {
          default = pkgs.mkShell {
            packages = builtins.attrValues {
              inherit (pkgs)
                rustc
                cargo
                clippy
                cargo-insta
                ;
              inherit rustfmt';
            };

            nativeBuildInputs = [
              pkgs.rustPlatform.bindgenHook
              pkgs.pkg-config
              pkgs.wrapGAppsHook4 # For `swayward-visual-tests`
            ];

            buildInputs = swayward.buildInputs ++ [
              pkgs.libadwaita # For `swayward-visual-tests`
            ];

            env = {
              # WARN: Do not overwrite this variable in your shell!
              # It is required for `dlopen()` to work on some libraries; see the comment
              # in the package expression
              #
              # This should only be set with `RUSTFLAGS="$RUSTFLAGS -C your-flags"`
              RUSTFLAGS = swayward.RUSTFLAGS;
            };
          };
        }
      );

      formatter = forAllSystems (system: nixpkgsFor.${system}.nixfmt-rfc-style);

      packages = forAllSystems (
        system:
        let
          swayward = nixpkgsFor.${system}.callPackage swayward-package { };
        in
        {
          inherit swayward;

          # NOTE: This is for development purposes only
          #
          # It is primarily to help with quickly iterating on
          # changes made to the above expression - though it is
          # also not stripped in order to better debug swayward itself
          swayward-debug = swayward.overrideAttrs (
            newAttrs: oldAttrs: {
              pname = oldAttrs.pname + "-debug";

              cargoBuildType = "debug";
              cargoCheckType = newAttrs.cargoBuildType;

              dontStrip = true;
            }
          );

          default = swayward;
        }
      );

      overlays.default = final: _: {
        swayward = final.callPackage swayward-package { };
      };
    };
}
